
def getToolchainFile() {
    if (project.hasProperty('TOOLCHAIN')) {
        return "-DCMAKE_TOOLCHAIN_FILE=${rootProject.file(project.property('TOOLCHAIN'))}"
    }
    return null
}

def getAdditionalProperties() {
    if (project.ext.has('CMAKE_PROPERTIES')) {
        return (String) project.ext.get('CMAKE_PROPERTIES')
    }
    return null
}


task deleteBuildDirectory(type: Delete) {
    delete project.file('build').absolutePath
}

task createBuildDirectory(type: Exec, dependsOn: deleteBuildDirectory) {
    commandLine 'mkdir', project.file('build').absolutePath
}

task runCmake(type: Exec, dependsOn: createBuildDirectory) {
    workingDir project.file('build').absolutePath

    def cmd = ['cmake']

    def toolchainFile = getToolchainFile()
    if (toolchainFile != null) {
        cmd.add toolchainFile
    }

    def additionalProperties = getAdditionalProperties()
    if (additionalProperties != null) {
        cmd.add additionalProperties
    }

    cmd.add '..'

    commandLine cmd
}

task runMake(type: Exec, dependsOn: runCmake) {
    workingDir project.file('build').absolutePath
    commandLine 'make'
}

task buildNatives {
    dependsOn tasks.runMake
}